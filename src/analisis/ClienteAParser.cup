package analisis;
import java_cup.runtime.*;
import java.util.*;

parser code

{:
    /**Metodo al que se llama automaticamente ante algun error sint√°ctico.*/
    public void syntax_error(Symbol s)
    {
        //Metodos.addError("sintactico", (s.right+1), s.left, "Identificador \"" +s.value + "\" no reconocido por la gramatica");
        System.out.println("Error sintactico en la Fila " + (s.right+1) +" Columna "+s.left+ ". Identificador \"" +s.value + "\" no reconocido." );
    }

    /**Metodo al que se llama en el momento en que ya no es posible una recuperacion de errores.*/ 
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception
    { 
        //Metodos.addError("sintactico", (s.right+1), s.left, "Identificador \"" +s.value + "\" no reconocido por la gramatica");
        System.out.println("Error sintactico en la Fila " + (s.right+1)+ " Columna "+s.left+". Identificador \"" + s.value + "\" no reconocido."); 
    }

:}

action code 
{:
    int nvl = 0;
    String nombreArchivo ="";
:} 

terminal tssesion, tesesion, tsusuario, teusuario, tsfecha, tefecha, tsnombre, tenombre;
terminal tscontenido, tecontenido, tsarchivos, tearchivos, tsdestinatario, tedestinatario, tsarchivo;
terminal tearchivo, tscelda, tecelda, tsformula, teformula, ttipo, ttama;
terminal tid, tfila, tcolumna, tcolor, tformula;
terminal tigual,tcadcampo, tcadena, tscorreo, tecorreo, tsde, tede;
terminal tamperson, toper, tpa, tpc, tbegin, tend, tpyc, tdospuntos, tcoma,tinteger; 
terminal tfloat, tchar, tboolean, tstring, tmas, tmenos, tpor, tdividir, tpot, tmod;
terminal tentero, tdecimal, tcaracter, ttrue , tfalse, tif, tla, tlc, telse;
terminal tor, tand, tno, tmenor, tmayor , tmenorigual, tmayorigual, tigualigual; 
terminal tdiferente, tfor, tto, tnext, twhile, tiden, treturn, tsasunto, teasunto;
terminal teregistro, tsregistro, tserror, teerror, tsenviado, teenviado;

non terminal INICIO, CUERPO, ELEMENTOS,SESION, LISTA_ATR_SESION, ATRIBUTO_SESION;
non terminal LISTA_CAMPOS, CAMPOS;
non terminal ARCHIVOS, LISTA_ARCHIVOS, ARCHIVO, LISTA_ATR_ARCHIVO, ATRIBUTO_ARCHIVO;
non terminal LISTA_CELDAS, CELDA, LISTA_ATR_CELDA, ATRIBUTOS_CELDA, CONTENIDO_CELDA;
non terminal FORMULA, INSTRUCCIONES;
non terminal CORREO;
non terminal LISTA_VARS, TIPO_VAR;
non terminal OPERACION, INSTRUCCION, DECLARA_VAR;
non terminal ASIGNA_VAR, REF_CELDA , EXPRESION, EXPR_PART, EXPR ,NUM,IF,ELSE;
non terminal CONDICIONES,CONDICION,OP_COMPARADOR,FOR,WHILE, RETURN, ENVIADO, LISTA_CAMPOS_CORREO;

precedence left tor;
precedence left tand;
precedence left tno, tigualigual;
precedence left tmenor, tmenorigual, tmayor, tmayorigual;

precedence left tmas, tmenos;
precedence left tpor, tdividir, tmod;
precedence left tpot;


start with INICIO; 

INICIO          ::= CUERPO {: System.out.println("Fin"); :};

CUERPO          ::= ELEMENTOS;

ELEMENTOS       ::= SESION
                    |CORREO;

SESION          ::= tssesion LISTA_ATR_SESION tcadcampo LISTA_CAMPOS tesesion;

LISTA_ATR_SESION::= LISTA_ATR_SESION ATRIBUTO_SESION
                    |ATRIBUTO_SESION;

ATRIBUTO_SESION ::= tid tigual tcadena
                    |ttipo tigual tcadena;

LISTA_CAMPOS    ::= LISTA_CAMPOS CAMPOS
                    |CAMPOS;

CAMPOS          ::=  tsusuario tcadcampo teusuario
                    |tsfecha tcadcampo tefecha
                    |tscontenido tcadcampo tecontenido
                    |tsasunto tcadcampo teasunto
                    |tsde tcadcampo tede
                    |tsnombre tcadcampo tenombre
                    |tsdestinatario tcadcampo tedestinatario
                    |tsregistro tcadcampo teregistro
                    |tserror tcadcampo teerror
                    |ARCHIVOS
                    |ENVIADO
                    |CORREO;

ENVIADO         ::= tsenviado tcadcampo LISTA_CAMPOS teenviado;


/**
LISTA_DESTINATARIOS ::= LISTA_DESTINATARIOS DESTINATARIO
                        |DESTINATARIO;
*/
                                    /** Verifica la validez del email en el sintactico para poder manejar mas facil el dominio del servidor */
/**
DESTINATARIO    ::= tsdestinatario tcadcampo tedestinatario;
*/

ARCHIVOS        ::= tsarchivos LISTA_ARCHIVOS tearchivos;

                    /** La lista puede venir vacia asi que lo dejo con un epsilon. */
LISTA_ARCHIVOS  ::= LISTA_ARCHIVOS ARCHIVO
                    |;

ARCHIVO         ::= tsarchivo LISTA_ATR_ARCHIVO tcadcampo LISTA_CELDAS tearchivo;

LISTA_ATR_ARCHIVO::= LISTA_ATR_ARCHIVO ATRIBUTO_ARCHIVO
                    |ATRIBUTO_ARCHIVO;

ATRIBUTO_ARCHIVO::= ttipo tigual tcadena
                    |ttama tigual tcadena;

LISTA_CELDAS    ::= LISTA_CELDAS CELDA
                    |CELDA;

CELDA           ::= tscelda LISTA_ATR_CELDA tcadcampo CONTENIDO_CELDA tecelda;

LISTA_ATR_CELDA ::= LISTA_ATR_CELDA ATRIBUTOS_CELDA
                    |ATRIBUTOS_CELDA;

ATRIBUTOS_CELDA ::= tfila tigual tcadena
                    |tcolumna tigual tcadena
                    |ttipo tigual tcadena
                    |tcolor tigual tcadena
                    |tformula tigual tcadena;


CONTENIDO_CELDA ::= tcadena
                    |FORMULA
                    |;

FORMULA         ::= tsformula OPERACION teformula;

CORREO          ::= tscorreo tcadcampo LISTA_CAMPOS_CORREO tecorreo;
 
LISTA_CAMPOS_CORREO ::= LISTA_CAMPOS
                        |;

OPERACION       ::= tamperson toper tigual tpa tbegin tla INSTRUCCIONES tlc tend tpc tpyc;

INSTRUCCIONES   ::= INSTRUCCIONES INSTRUCCION
                    |INSTRUCCION;

INSTRUCCION     ::= DECLARA_VAR 
                    |ASIGNA_VAR
                    |IF
                    |FOR
                    |WHILE
                    |RETURN;

RETURN          ::= treturn EXPRESION;

DECLARA_VAR     ::= LISTA_VARS tdospuntos TIPO_VAR;

LISTA_VARS      ::= LISTA_VARS tcoma tiden
                    |tiden;

TIPO_VAR        ::= tinteger
                    |tfloat
                    |tchar
                    |tboolean
                    |tstring;

ASIGNA_VAR      ::= tiden tigual EXPRESION
                    |REF_CELDA tigual EXPRESION;

REF_CELDA       ::= tamperson tpa EXPRESION tcoma EXPRESION tpc;

EXPRESION       ::= EXPR_PART;

EXPR_PART       ::= EXPR;

EXPR            ::= EXPR:e1 tmas EXPR:e2  
                    |EXPR:e1 tmenos EXPR:e2  
                    |EXPR:e1 tpor EXPR:e2  
                    |EXPR:e1 tdividir EXPR:e2 
                    |EXPR:e1 tpot EXPR:e2
                    |EXPR:e1 tmod EXPR:e2
                    |NUM:n   
                    |tpa EXPR:e tpc;

NUM             ::= tentero:e 
                    |tdecimal:e
                    |tcadena:e 
                    |tcaracter:e
                    |ttrue:e
                    |tfalse:e
                    |tiden:e
                    |REF_CELDA; 

IF              ::= tif tpa CONDICIONES tpc tla INSTRUCCIONES tlc ELSE;

ELSE            ::= telse tla INSTRUCCIONES:l tlc {: RESULT = l; :}
                    |{: RESULT = null; :};

CONDICIONES     ::= CONDICIONES:e1 tor CONDICIONES:e2 
                    |CONDICIONES:e1 tand CONDICIONES:e2 
                    |tno tpa CONDICIONES:e tpc
                    |tpa CONDICIONES:e tpc 
                    |CONDICION:e ;

CONDICION       ::= EXPRESION:val1 OP_COMPARADOR:op EXPRESION:val2 ;


OP_COMPARADOR   ::= tmenor          {: RESULT = new String ("<"); :}
                    |tmayor         {: RESULT = new String (">"); :}
                    |tmenorigual    {: RESULT = new String ("<="); :}
                    |tmayorigual    {: RESULT = new String (">="); :}
                    |tigualigual    {: RESULT = new String ("=="); :}
                    |tdiferente     {: RESULT = new String ("<>"); :};

FOR             ::= tfor tiden tigual EXPRESION tto EXPRESION tnext tla INSTRUCCIONES tlc;

WHILE           ::= twhile tpa CONDICIONES tpc tla INSTRUCCIONES tlc ;



 

